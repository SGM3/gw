buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
		classpath group: "com.jfrog.bintray.gradle", name: "gradle-bintray-plugin", version: "1.7.3"
		classpath "gradle.plugin.com.palantir.graal:gradle-graal:0.6.0-112-gca0b727"
	}

	repositories {
		mavenCentral()
		jcenter()

		maven {
	      url "https://plugins.gradle.org/m2/"
	    }
	}
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.liferay.source.formatter'
apply plugin: 'com.palantir.graal'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
	mavenCentral()
}

dependencies {
	testCompile group: "junit", name: "junit", version: "4.12"
}

formatSource {
	maxLineLength = 120
}

jar {
	manifest {
		attributes(
			"Bundle-Version": project.version,
			"JPM-Command": "gw",
			"Main-Class": "com.liferay.gw.GradleWrapper"
		)
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task mavenWrapperJar(type: Jar, dependsOn: compileJava) {
	from sourceSets.main.output
	archiveName = "mw.jar"
	manifest {
		attributes(
			"Bundle-Version": project.version,
			"JPM-Command": "mw",
			"Main-Class": "com.liferay.gw.MavenWrapper"
		)
	}
}

assemble.dependsOn mavenWrapperJar

artifacts {
	archives sourcesJar
	archives javadocJar
}

graal {
	mainClass "com.liferay.gw.GradleWrapper"
	outputName 'gw'
}

publishing {
	publications {
		GwPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			artifactId 'gw'
		}
		MwPublication(MavenPublication) {
			from components.java
			artifact mavenWrapperJar
			artifactId 'mw'
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['GwPublication', 'MwPublication']
	pkg {
		repo = 'maven'
		name = 'gw'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/david-truong/gw.git'
		labels = ['gradle', 'gradle_wrapper']
		version {
			name = project.version
			desc = 'Universal Gradle Wrapper command'
			vcsTag = project.version
		}
	}
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"
group = "com.liferay"
version = '1.0.3-SNAPSHOT'
