buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.0.0'
	}
}

plugins {
	id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'idea'

repositories {
	jcenter()
}

dependencies {
	testCompile 'junit:junit:4.12'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

task release(type: Copy) {
	from jar
	into 'release'
}

build {
	dependsOn release
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId 'com.liferay'
			artifactId 'gw'
			version '1.0.1'
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['MyPublication']
	pkg {
		repo = 'maven'
		name = 'gw'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/david-truong/gw.git'
		labels = ['gradle', 'gradle_wrapper']
		version {
			name = '1.0.1'
			desc = 'Universal Gradle Wrapper command'
			vcsTag = '1.0.1'
		}
	}
}
